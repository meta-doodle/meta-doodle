grammar org.xtext.metadoodle.MDL with org.eclipse.xtext.common.Terminals

generate mDL "http//www.xtext.org/metadoodle/MDL"


// Lan => Language
WorkflowLan:
	//'MDLWorkflow' name=ID
	//'Description' Desc=STRING
	'FirstStep' firstStep=[WorkflowStepLan]
	('Roles' (roles+=RoleLan)+ )?
	'Steps' (steps+=WorkflowStepLan)+
;

WorkflowStepLan :
    '{' 'StepName' name=ID
    //'Comment' comment=STRING
    ('Role' role=[RoleLan])?
    ('NextStep' nextStep=[WorkflowStepLan])?
    (userInteraction+=UserInteractionLan)?
    synchro=SynchroLan
    (reminders=MailReminderLan)? '}'
;

UserInteractionLan :
	SurveyLan | CalendarLan | FileUploadLan
;

SurveyLan:
	'Survey'
	'{' //('isAnonimousAnswer' isAnonimousAnswer=BOOLEAN)? 
	'Questions' (questions+=StandardQuestionLan)* 
	questions+=( AbrstractQuest ) '}'
	// IsAnonimous ne sera pas pris en compte tout de suite.
;

AbrstractQuest:
	StandardQuestionLan | GotoQuestionLan
;

StandardQuestionLan:
	'{' 'QuestionID' id=ID
	'QuestionTitle' title=STRING
    'AnswerType' answerType=AnswerTypeLan
    //('MaxPossibleAnswer' MaxPossibleAnswer=INT)?
    ('PossibleAnswers' '(' (responses+=STRING)+ ')')? '}'
	// maxPossibleAnswer ne sera pas pris en compte tout de suite.
;

GotoQuestionLan:
	'{' 'QuestionID' id=ID
	'QuestionTitle' title=STRING
    'PossibleAnswers' '(' (gotoNextStep+=PossibleAnswerLan)+ ')' '}'
;

PossibleAnswerLan:
	('NextStep' nextStep=[WorkflowStepLan])? response=STRING
;

AnswerTypeLan:
    Type = ('OpenQuestion' | 'CheckBox' | 'RadioButton')
;

CalendarLan:
	'Calendar' id=ID
    '{' 'StartingDate' startDate=DATE
    'EndingDate' endDate=DATE 
    ('Accuracy' num=INT ':' unit=UnitLan)?
    //('MaxPossibleAnswer' maxPossibleAnswer=INT)?
    /*('MaxPeopleBySlot' maxPeopleBySlot=INT)?*/ '}'
	// maxPossibleAnswer et maxPeopleBySlot ne seront pas pris en compte tout de suite.
;

UnitLan:
	unit = ('Minute' | 'Hour' | 'Day' | 'Week' | 'Month')
;

FileUploadLan:
	'FileUpload' id=ID
    '{' ('SizeMaxInOctet' maxSize=INT)?
   	'MimeTypes' (mimeTypes+=STRING)+ '}'
;

SynchroLan:
	'Synchro'
    '{' ('EndStepDate' endStepDate=DATE)?
    // If we have a certain percentage of answer
    'PercentCompletionNeeded' percentageCompletionNeeded=INT '}'
;

MailReminderLan:
    'MailReminder'
    '{' 'Object' object=STRING
    'Body' body=STRING 
    'Dates' (dateToSend+=DATE)+ '}'
;

//DateIDLan: '(' id=ID desc=STRING ')';
RoleLan: name=(ID | 'All');

// Lexing
terminal BOOLEAN: 'true' | 'false';
terminal DATE :
	('0'..'3')('0'..'9')'/'('0'..'1')('0'..'9')'/'(('2'..'9')('0'..'9'))?('0'..'9')('0'..'9')
;