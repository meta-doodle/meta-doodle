grammar org.xtext.metadoodle.MDL with org.eclipse.xtext.common.Terminals

generate mDL "http://www.xtext.org/metadoodle/MDL"

// Lan => Language
WorkflowLan:
	'MDLWorkflow:' name=ID
	Desc=STRING
	'Roles:' (roles+=RoleLan)+
	'Steps:' (steps+=WorkflowStepLan)+
;

WorkflowStepLan :
    '{' 'StepName:' name=StepIDLan
    'Comment:' comment=STRING
    'Role:' role=[RoleLan]
    'NextStep' nextStep=[StepIDLan]
    ( userInteraction+=UserInteractionLan )?
    synchro=SynchroLan
    ('(' reminders=MailReminderLan ')')? '}'
;

UserInteractionLan :
	SurveyLan | CalendarLan | FileUploadLan
;

SurveyLan:
	'Survey'
	'{' ('isAnonimousAnswer:' isAnonimousAnswer=BOOLEAN)? 
	(questions+=StandardQuestionLan)* 
	questions+=( StandardQuestionLan | GotoQuestionLan ) '}'
	// IsAnonimous ne sera pas pris en compte tout de suite.
;

StandardQuestionLan:
	'QuestionID:' id=QuestionIDLan
	'QuestionTitle' title=STRING
    ('Role:' role=[RoleLan] )?
    'AnswerType:' answerType=AnswerTypeLan
    'NextQuestion:' nextQuestion=[QuestionIDLan]
    ('MaxPossibleAnswer' MaxPossibleAnswer=INT)?
    ('(' 'PossibleAnswers:' (responses+=STRING)+ ')')?
	// maxPossibleAnswer ne sera pas pris en compte tout de suite.
;

GotoQuestionLan:
	'QuestionID:' id=QuestionIDLan
	'QuestionTitle' title=STRING
    ('Role:' role=[RoleLan] )?
    '(' 'PossibleAnswers:' (
    	'NextStep:' nextID+=[StepIDLan]
    	responses+=STRING
    )+ ')'
;

AnswerTypeLan:
    Type = ('OpenQuestion' | 'CheckBox' | 'RadioButton')
;

CalendarLan:
	'Calendar'
    '{' 'StartingDate:' startDate=DateIDLan
    'EndingDate:' endDate=DateIDLan 
    ('Accuracy:' num=INT ':' unit=UnitLan)?
    ('MaxPossibleAnswer' maxPossibleAnswer=INT)?
    ('MaxPeopleBySlot' maxPeopleBySlot=INT)? '}'
	// maxPossibleAnswer et maxPeopleBySlot ne seront pas pris en compte tout de suite.
;

UnitLan:
	unit = ('Minute' | 'Hour' | 'Day' | 'Week' | 'Month')
;

FileUploadLan:
	'FileUpload'
    '{' ('SizeMaxInOctet:' maxSize=INT)?
   	'MimeTypes:' (mimeTypes+=STRING)+ '}'
;

SynchroLan:
	'Synchro'
    endStepDate=DateIDLan
    // If we have a certain percentage of answer
    percentageCompletionNeeded=INT
;

MailReminderLan:
    '{' 'MailReminder'
    object=STRING
    body=STRING 
    (dateToSend+=DateIDLan)+ '}'
;

StepIDLan: name=STRING;
QuestionIDLan: name=STRING;
DateIDLan: id=STRING desc=STRING;
RoleLan: name=(STRING | 'All');

// Lexing
terminal BOOLEAN: 'true' | 'false';