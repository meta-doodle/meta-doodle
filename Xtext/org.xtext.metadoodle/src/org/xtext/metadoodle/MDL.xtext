grammar org.xtext.metadoodle.MDL with org.eclipse.xtext.common.Terminals

generate mDL "http://www.xtext.org/metadoodle/MDL"

// Parsing
WorkflowLan:
	name=ID
	Desc=STRING
	(steps+=WorkflowStepLan)+
;

WorkflowStepLan :
    '{' 'StepName:' name=ID
    'Comment:' comment=STRING
    userInteraction+=UserInteractionLan
    synchro=SynchroLan
    ('(' reminders=MailReminderLan ')')? '}'
;

UserInteractionLan :
	interaction = SurveyLan | CalendarLan | FileUploadLan
;

SurveyLan:
	'Survey'
	'{' (questions+=QuestionLan)+ '}'
;

QuestionLan:
	'QuestionTitle:' name=ID
    'QuestionType:' type=AnswerTypeLan
    ('PossibleAnswers:' (responses+=STRING)+)?
;

AnswerTypeLan:
    type=('OpenQuestion' | 'CheckBox' | 'RadioButton')
;

CalendarLan:
	'Calendar'
    '{' 'StartingDate:' start=DATE
    'EndingDate:' end=DATE 
    ('Accuracy:' accuracy=STRING)? '}'
;

FileUploadLan:
	'FileUpload'
    '{' (mimeTypes+=STRING)+ '}'
;

SynchroLan:
	'Synchro'
    endDate=DATE
    waitOtherUsers=BOOLEAN
    // If we have a certain percentage of answer
    percentageCompletionUse=BOOLEAN
    percentageCompletion=INT
;

MailReminderLan:
    'MailReminder'
    (dateToSend+=DATE)+
    object=STRING
    body=STRING
;

// Lexing
terminal DATE:
    ('0'..'3')('0'..'9')'/'('0'..'1')('0'..'9')'/'(('2'..'9')('0'..'9'))?('0'..'9')('0'..'9')
;

terminal BOOLEAN: 'true' | 'false';